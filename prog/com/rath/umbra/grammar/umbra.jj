options
{
  STATIC = false;
  DEBUG_PARSER = true;
}

// --------------------------------------------------------------------------------------------------------------------
// Parser Declaration

PARSER_BEGIN(UmbraParser)

package com.rath.umbra.grammar;

import java.io.*;
import java.util.List;
import java.util.ArrayList;

import com.rath.umbra.entity.*;

public class UmbraParser {

  public Program buildTree() {
    
    try {
      System.out.println("Building parse tree...");
      return PROGRAM();
    } catch (TokenMgrError tme) {
      System.err.println(tme.getMessage());
      return null;
    } catch (ParseException pe) {
      System.err.println(pe.getMessage());
      return null;
    }
  }

}

PARSER_END(UmbraParser)

// --------------------------------------------------------------------------------------------------------------------
// Tokens

SKIP: {
  " "| "\t" | "\n" | "\r" | <"#" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

// --------------------------------------------------------------------------------------------------------------------
// Separators / Misc tokens
TOKEN: {
  
  "{" | "}" | "[" | "]" | "," | ";" | ":" | "\"" | "@" | "."
  
}

// --------------------------------------------------------------------------------------------------------------------
// Reserved Words
TOKEN: {
  
  "var" | "set" | "param" | "if" | "else" | "main" | "func" | "for" | "return" | "while"
  
}

// --------------------------------------------------------------------------------------------------------------------
// Values

TOKEN: {
  
  < IDENTIFIER: ["a"-"z","A"-"Z"] (["a"-"z","A"-"Z","0"-"9","_"])*  >
|  
  < REFERENCE: "@" <IDENTIFIER> ":" <IDENTIFIER> >
|
  < LIT_GENERAL: <LIT_NUMBER> | <LIT_CHAR> | <LIT_STRING> >
|
  < LIT_NUMBER: <LIT_DECIMAL> | <LIT_HEX> >
|
  < #LIT_DECIMAL: (["1"-"9"] (["0"-"9"])*) | "0" >
|
  < #LIT_HEX: "0x" (["0"-"9","a"-"f","A"-"F"]){1,4} >
|
  < LIT_CHAR:
    "'"
    (   
      (~["\"","\\","\n"]) | 
      ("\\" ( ["n","\\","'","\""]))
    )*
    "'"
  >
|
  < LIT_STRING:
    "\""
    (   
      (~["\"","\\","\n"]) | 
      ("\\" ( ["n","\\","'","\""]))
    )*
    "\""
  >
  
  
}


// --------------------------------------------------------------------------------------------------------------------
// Grammar

Program PROGRAM() :
{
  List<ParamDeclaration> paramDeclList = new ArrayList<ParamDeclaration>();
  List<FunctionDeclaration> funcDeclList = new ArrayList<FunctionDeclaration>();
  MainDeclaration mainDecl;
  ParamDeclaration pd;
  FunctionDeclaration fd;
}
{

  ( pd = PARAM_DECLARATION() { paramDeclList.add(pd); } )*
  ( mainDecl = MAIN_DECLARATION() )
  ( fd = FUNCTION_DECLARATION() {funcDeclList.add(fd); } )*
  <EOF>
  { return new Program(paramDeclList, funcDeclList, mainDecl); }
}

MainDeclaration MAIN_DECLARATION() :
{
  StatementList body;
}
{
  "main" "{"
    ( body = STATEMENT_LIST() )
  "}"
  
  { return new MainDeclaration(body); }
}

FunctionDeclaration FUNCTION_DECLARATION() :
{
  Token name;
  ParamList paramList;
  StatementList body;
}
{
  "func" name = <IDENTIFIER> "(" paramList = PARAM_LIST() ")" "{"
    body = STATEMENT_LIST()
  "}"
  
  { return new FunctionDeclaration(body, name.image, paramList); }
}

ParamDeclaration PARAM_DECLARATION() :
{
  Token id;
  Token value;
}
{
 
  "param" (id = <IDENTIFIER>) "=" (value = <LIT_GENERAL>) ";"
  
  { return new ParamDeclaration(id.image, value.image); }
}

ParamList PARAM_LIST() :
{
  ParamListHead head;
  List<ParamListTail> tail = new ArrayList<ParamListTail>();
  ParamListTail t;
}
{
  head = PARAM_LIST_HEAD() ( t = PARAM_LIST_TAIL() { tail.add(t); } )*
  
  { return new ParamList(head, tail); }
}

ParamListHead PARAM_LIST_HEAD() :
{
  Token id;
}
{
  "var" id = <IDENTIFIER>
  
  { return new ParamListHead(id.image); }
}

ParamListTail PARAM_LIST_TAIL() :
{
  Token id;
}
{
  "," "var" id = <IDENTIFIER>
  
  { return new ParamListTail(id.image); }
}

StatementList STATEMENT_LIST() :
{
  List<Statement> list = new ArrayList<Statement>();
  Statement st;
}
{
  (LOOKAHEAD(2) st = STATEMENT() { list.add(st); } )+
  { return new StatementList(list); }
}

Statement STATEMENT() :
{
  Operation operation;
  ControlStructure controlStructure;
  Expression expr;
}
{
  ( operation = OPERATION() ";")
  { return operation; }
|
  ( controlStructure = CONTROL_STRUCTURE() )
  { return controlStructure; }
|
  ( expr = EXPRESSION() ";" )
  { return expr; }
}

Operation OPERATION(): 
{
  SetOperation setOperation;
  VarDeclaration varDeclaration;
  VarInitialization varInitialization;
  ReturnOperation returnOperation;
}
{
  (
    LOOKAHEAD(3) setOperation = SET_OPERATION()
    { return new SetOperation(setOperation.getVar(), setOperation.getExpr()); }
  |
    LOOKAHEAD(3) varInitialization = VAR_INITIALIZATION()
    { return new VarInitialization(varInitialization.getVar(), varInitialization.getExpr()); }
  |
    LOOKAHEAD(3) varDeclaration = VAR_DECLARATION()
    { return new VarDeclaration(varDeclaration.getName()); }
  |
    returnOperation = RETURN_OPERATION()
    { return new ReturnOperation(returnOperation.getExpr()); }
  )
}

ControlStructure CONTROL_STRUCTURE() :
{
   ConditionalBranch conditionalBranch;
   WhileLoop whileLoop;
   ForLoop forLoop;
}
{
  conditionalBranch = CONDITIONAL_BRANCH()
  { return new ConditionalBranch(conditionalBranch.getIfExpr(), conditionalBranch.getElseifExprList(), conditionalBranch.getElseExpr(), conditionalBranch.getIfBody(), conditionalBranch.getElseifBodyList(), conditionalBranch.getElseBody()); }
|
  whileLoop = WHILE_LOOP()
  { return new WhileLoop(whileLoop.getExpr(), whileLoop.getBody()); }
|
  forLoop = FOR_LOOP()
  { return new ForLoop(forLoop.getInit(), forLoop.getCond(), forLoop.getSetOp(), forLoop.getBody()); }
}

SetOperation SET_OPERATION() :
{
  Token id;
  Expression expr;
}
{
  "set" id = <IDENTIFIER> "=" expr = EXPRESSION()
  { return new SetOperation(id.image, expr); }
}

VarDeclaration VAR_DECLARATION() :
{
  Token id;
}
{
  "var" id = <IDENTIFIER>
  { return new VarDeclaration(id.image); }
}

VarInitialization VAR_INITIALIZATION() :
{
  Token id;
  Expression expr;
}
{
  "var" id = <IDENTIFIER> "=" expr = EXPRESSION()
  { return new VarInitialization(id.image, expr); }
}

ReturnOperation RETURN_OPERATION() :
{
  Expression expr = null;
}
{
  "return" (expr = EXPRESSION())?
  { return new ReturnOperation(expr); }
}

ConditionalBranch CONDITIONAL_BRANCH() :
{
  Expression ifExpr;
  List<Expression> elsifExprList = new ArrayList<Expression>();
  Expression elseExpr = null;
  
  StatementList ifBody;
  List<StatementList> elsifStatementBodyList = new ArrayList<StatementList>();
  StatementList elseBody = null;
  
  Expression elsifExpr;
  StatementList elsifStatementList;
}
{
  "if" "(" ifExpr = EXPRESSION() ")" "{"
    ifBody = STATEMENT_LIST()
  "}"
  
  
  ( LOOKAHEAD(2) 
    "else" "if" "(" elsifExpr = EXPRESSION() { elsifExprList.add(elsifExpr); } ")" "{"
      elsifStatementList = STATEMENT_LIST() { elsifStatementBodyList.add(elsifStatementList); }
    "}"
  )*
  
  (
    "else" "(" elseExpr = EXPRESSION() ")" "{"
      elseBody = STATEMENT_LIST()
    "}"
  )?
  
  { return new ConditionalBranch(ifExpr, elsifExprList, elseExpr, ifBody, elsifStatementBodyList, elseBody); }
}

WhileLoop WHILE_LOOP() :
{
  Expression conditional;
  StatementList body;
}
{
  "while" "(" conditional = EXPRESSION() ")" "{"
    body = STATEMENT_LIST()
  "}"
  
  { return new WhileLoop(conditional, body); }
}

ForLoop FOR_LOOP() :
{
  VarInitialization init = null;
  Expression conditional;
  SetOperation setOperation = null;
  StatementList body;
}
{
  "for" "(" (init = VAR_INITIALIZATION())? ";" conditional = EXPRESSION() ";" (setOperation = SET_OPERATION())? ")" "{"
    body = STATEMENT_LIST()
  "}"
  
  { return new ForLoop(init, conditional, setOperation, body); }
}

Expression EXPRESSION() :
{
  FunctionCall fc;
  BinaryOperation bo;
  UnaryOperation uo;
}
{
  fc = FUNCTION_CALL()
  {return new FunctionCall(fc.getName(), fc.getArgs());}
|
  LOOKAHEAD(3) bo = BINARY_OPERATION()
  {return bo;}
|
  uo = UNARY_OPERATION()
  {return uo;}
}

UnaryOperation UNARY_OPERATION() :
{
  Token op;
  Expression expr;
  VariableReference vr;
  NumberVal n;
}
{
  op = <("!" | "-")> expr = EXPRESSION()
  {return new UnaryOperation(op.image, expr);}
|
  vr = VARIABLE_REFERENCE()
  {return vr;}
|
  n = NUMBER()
  {return n;}
}

BinaryOperation BINARY_OPERATION() :
{
  Token op;
  Expression expr;
  UnaryOperation uo;
}
{
  uo = UNARY_OPERATION() op = <("<<<" | ">>>" | "==" | "<=" | ">=" | "<<" | ">>" | "!=" | "+" | "-" | "*" | "/" | "%" | "^" | ">" | "<")> expr = EXPRESSION()
  {return new BinaryOperation(uo, op.image, expr);}
}

VariableReference VARIABLE_REFERENCE() :
{
  Token t;
}
{
  t = <IDENTIFIER>
  {return new VariableReference(t.image);}
}

NumberVal NUMBER() :
{
  Token t;
}
{
  t = <LIT_GENERAL>
  {return new NumberVal(t.image);}
}

FunctionCall FUNCTION_CALL() :
{
  Token name;
  ArgList argList = null;
}
{
  "." name = <IDENTIFIER> "(" ( argList = ARG_LIST() )? ")"
  { return new FunctionCall(name.image, ((argList == null) ? null : argList.getList())); }
}

ArgList ARG_LIST() :
{
  Expression expr;
  List<Expression> exprList = new ArrayList<Expression>();
}
{
  expr = EXPRESSION() {exprList.add(expr);} ("," expr = EXPRESSION() { exprList.add(expr); } )*
  { return new ArgList(exprList); }
}

