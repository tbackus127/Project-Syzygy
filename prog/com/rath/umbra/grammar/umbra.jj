options
{
  STATIC = false;
  DEBUG_PARSER = true;
}

// --------------------------------------------------------------------------------------------------------------------
// Parser Declaration

PARSER_BEGIN(UmbraParser)

package com.rath.umbra.grammar;

import java.io.*;
import java.util.List;
import java.util.ArrayList;

import com.rath.umbra.entity.*;

public class UmbraParser {

  public Program buildTree() {
    
    try {
      System.out.println("Building parse tree...");
      return PROGRAM();
    } catch (TokenMgrError tme) {
      System.err.println(tme.getMessage());
      return null;
    } catch (ParseException pe) {
      System.err.println(pe.getMessage());
      return null;
    }
  }

}

PARSER_END(UmbraParser)

// --------------------------------------------------------------------------------------------------------------------
// Tokens

SKIP: {
  " "| "\t" | "\n" | "\r" | <"#" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

// --------------------------------------------------------------------------------------------------------------------
// Separators / Misc tokens
TOKEN: {
  
  "{" | "}" | "[" | "]" | "," | ";" | ":" | "\"" | "@" | "."
  
}

// --------------------------------------------------------------------------------------------------------------------
// Reserved Words
TOKEN: {
  
  "var" | "set" | "param" | "if" | "else" | "main" | "func" | "for" | "return" | "while"
  
}

// --------------------------------------------------------------------------------------------------------------------
// Values

TOKEN: {
  
  < IDENTIFIER: ["a"-"z","A"-"Z"] (["a"-"z","A"-"Z","0"-"9","_"])*  >
|  
  < LIT_GENERAL: <LIT_NUMBER> | <LIT_CHAR> | <LIT_STRING> >
|
  < LIT_NUMBER: <LIT_DECIMAL> | <LIT_HEX> >
|
  < #LIT_DECIMAL: (["1"-"9"] (["0"-"9"])*) | "0" >
|
  < #LIT_HEX: "0x" (["0"-"9","a"-"f","A"-"F"]){1,4} >
|
  < LIT_CHAR:
    "'"
    (   
      (~["\"","\\","\n"]) | 
      ("\\" ( ["n","\\","'","\""]))
    )*
    "'"
  >
|
  < LIT_STRING:
    "\""
    (   
      (~["\"","\\","\n"]) | 
      ("\\" ( ["n","\\","'","\""]))
    )*
    "\""
  >
  
  
}


// --------------------------------------------------------------------------------------------------------------------
// Grammar
// --------------------------------------------------------------------------------------------------------------------

// --------------------------------------------------------------------------------------------------------------------
// Program:
//   <PARAM_DECLARATION>*
//   <MAIN_DECLARATION>         (order matters; params, main, functions)
//   <FUNCTION_DECLARATION>*
Program PROGRAM() :
{
  List<ParamDeclaration> paramDeclList = new ArrayList<ParamDeclaration>();
  List<FunctionDeclaration> funcDeclList = new ArrayList<FunctionDeclaration>();
  MainDeclaration mainDecl;
  ParamDeclaration pd;
  FunctionDeclaration fd;
}
{

  ( pd = PARAM_DECLARATION() { paramDeclList.add(pd); } )*
  ( mainDecl = MAIN_DECLARATION() )
  ( fd = FUNCTION_DECLARATION() {funcDeclList.add(fd); } )*
  <EOF>
  { return new Program(paramDeclList, funcDeclList, mainDecl); }
}

// --------------------------------------------------------------------------------------------------------------------
// Main declaration:
// main {
//   <STATEMENT_LIST> 
// }
MainDeclaration MAIN_DECLARATION() :
{
  StatementList body;
}
{
  "main" "{"
    ( body = STATEMENT_LIST() )
  "}"
  
  { return new MainDeclaration(body); }
}

// --------------------------------------------------------------------------------------------------------------------
// Function declaration:
// func NAME ( <PARAM_LIST> ) {
//   <STATEMENT_LIST>  
// } 
FunctionDeclaration FUNCTION_DECLARATION() :
{
  Token name;
  ParamList paramList;
  StatementList body;
}
{
  "func" name = <IDENTIFIER> "(" paramList = PARAM_LIST() ")" "{"
    body = STATEMENT_LIST()
  "}"
  
  { return new FunctionDeclaration(body, name.image, paramList); }
}

// --------------------------------------------------------------------------------------------------------------------
// Parameter declaration:
// param IDENTIFIER = LITERAL;
ParamDeclaration PARAM_DECLARATION() :
{
  Token id;
  Token value;
}
{
 
  "param" (id = <IDENTIFIER>) "=" (value = <LIT_GENERAL>) ";"
  
  { return new ParamDeclaration(id.image, value.image); }
}

// --------------------------------------------------------------------------------------------------------------------
// Parameter list
// (var NAME, var NAME, var Name, ...), or empty
ParamList PARAM_LIST() :
{
  ParamListHead head;
  List<ParamListTail> tail = new ArrayList<ParamListTail>();
  ParamListTail t;
}
{
  head = PARAM_LIST_HEAD() ( t = PARAM_LIST_TAIL() { tail.add(t); } )*
  
  { return new ParamList(head, tail); }
}

// --------------------------------------------------------------------------------------------------------------------
// Parameter list head, internal parser structure
ParamListHead PARAM_LIST_HEAD() :
{
  Token id = null;
}
{
  ("var" id = <IDENTIFIER>)?
  
  { return new ParamListHead((id == null) ? null : id.image); }
}

// --------------------------------------------------------------------------------------------------------------------
// Parameter list tail, internal parser structure
ParamListTail PARAM_LIST_TAIL() :
{
  Token id;
}
{
  "," "var" id = <IDENTIFIER>
  
  { return new ParamListTail(id.image); }
}

// --------------------------------------------------------------------------------------------------------------------
// Statement list, internal parser structure
StatementList STATEMENT_LIST() :
{
  List<Statement> list = new ArrayList<Statement>();
  Statement st;
}
{
  (LOOKAHEAD(2) st = STATEMENT() { list.add(st); } )*
  { return new StatementList(list); }
}

// --------------------------------------------------------------------------------------------------------------------
// Statement, can be either a control structure (loop, if-else), an operation (set, function call), or an expression.
Statement STATEMENT() :
{
  Operation operation;
  ControlStructure controlStructure;
  Expression expr;
}
{
  ( operation = OPERATION() ";")
  { return operation; }
|
  ( controlStructure = CONTROL_STRUCTURE() )
  { return controlStructure; }
|
  ( expr = EXPRESSION() ";" )
  { return expr; }
}

// --------------------------------------------------------------------------------------------------------------------
// Operation, can declare/initialize/set a variable, or return a value.
Operation OPERATION(): 
{
  SetOperation setOperation;
  VarDeclaration varDeclaration;
  VarInitialization varInitialization;
  ReturnOperation returnOperation;
}
{
  (
    LOOKAHEAD(3) setOperation = SET_OPERATION()
    { return setOperation; }
  |
    LOOKAHEAD(3) varInitialization = VAR_INITIALIZATION()
    { return varInitialization; }
  |
    LOOKAHEAD(3) varDeclaration = VAR_DECLARATION()
    { return varDeclaration; }
  |
    returnOperation = RETURN_OPERATION()
    { return returnOperation; }
  )
}

// --------------------------------------------------------------------------------------------------------------------
// If-else and loops
ControlStructure CONTROL_STRUCTURE() :
{
   ConditionalBranch conditionalBranch;
   WhileLoop whileLoop;
   ForLoop forLoop;
}
{
  conditionalBranch = CONDITIONAL_BRANCH()
  { return new ConditionalBranch(conditionalBranch.getIfExpr(), conditionalBranch.getElseifExprList(), conditionalBranch.getElseExpr(), conditionalBranch.getIfBody(), conditionalBranch.getElseifBodyList(), conditionalBranch.getElseBody()); }
|
  whileLoop = WHILE_LOOP()
  { return new WhileLoop(whileLoop.getExpr(), whileLoop.getBody()); }
|
  forLoop = FOR_LOOP()
  { return new ForLoop(forLoop.getInit(), forLoop.getCond(), forLoop.getSetOp(), forLoop.getBody()); }
}

// --------------------------------------------------------------------------------------------------------------------
// Set operation
//   set VAR = EXPRESSION;
SetOperation SET_OPERATION() :
{
  VariableReference vr;
  ArrayExpression ae;
  Expression expr;
}
{
  "set" vr = VARIABLE_REFERENCE() "=" expr = EXPRESSION()
  { return new SetOperation(vr, expr); }
}

// --------------------------------------------------------------------------------------------------------------------
// Variable declaration
//   var NAME;
VarDeclaration VAR_DECLARATION() :
{
  Token id;
}
{
  "var" id = <IDENTIFIER>
  { return new VarDeclaration(id.image); }
}

// --------------------------------------------------------------------------------------------------------------------
// Declares and sets a variable
//   var NAME = EXPRESSION;
VarInitialization VAR_INITIALIZATION() :
{
  Token id;
  Expression expr;
}
{
  "var" id = <IDENTIFIER> "=" expr = EXPRESSION()
  { return new VarInitialization(id.image, expr); }
}

// --------------------------------------------------------------------------------------------------------------------
// Returns a value from a function call
//   return EXPRESSION;
ReturnOperation RETURN_OPERATION() :
{
  Expression expr = null;
}
{
  "return" (expr = EXPRESSION())?
  { return new ReturnOperation(expr); }
}

// --------------------------------------------------------------------------------------------------------------------
// If-else block
//   if(EXPRESSION) {
//     BODY 
//   } else if (EXPRESSION) {
//     BODY 
//   }
//   ...
//   } else {
//     BODY
//   }
ConditionalBranch CONDITIONAL_BRANCH() :
{
  Expression ifExpr;
  List<Expression> elsifExprList = new ArrayList<Expression>();
  Expression elseExpr = null;
  
  StatementList ifBody;
  List<StatementList> elsifStatementBodyList = new ArrayList<StatementList>();
  StatementList elseBody = null;
  
  Expression elsifExpr;
  StatementList elsifStatementList;
}
{
  "if" "(" ifExpr = EXPRESSION() ")" "{"
    ifBody = STATEMENT_LIST()
  "}"
  
  
  ( LOOKAHEAD(2) 
    "else" "if" "(" elsifExpr = EXPRESSION() { elsifExprList.add(elsifExpr); } ")" "{"
      elsifStatementList = STATEMENT_LIST() { elsifStatementBodyList.add(elsifStatementList); }
    "}"
  )*
  
  (
    "else" "{"
      elseBody = STATEMENT_LIST()
    "}"
  )?
  
  { return new ConditionalBranch(ifExpr, elsifExprList, elseExpr, ifBody, elsifStatementBodyList, elseBody); }
}

// --------------------------------------------------------------------------------------------------------------------
// While loop
//   while(EXPRESSION) {
//     BODY 
//   }
WhileLoop WHILE_LOOP() :
{
  Expression conditional;
  StatementList body;
}
{
  "while" "(" conditional = EXPRESSION() ")" "{"
    body = STATEMENT_LIST()
  "}"
  
  { return new WhileLoop(conditional, body); }
}

// --------------------------------------------------------------------------------------------------------------------
// For loop
//   for(VAR_INIT = EXPRESSION; EXPRESSION; SET_OP) {
//     BODY
//   }
// The initial step (first part delimited by ";") and update step (third) are optional, but the check (second) is not.
ForLoop FOR_LOOP() :
{
  VarInitialization init = null;
  Expression conditional;
  SetOperation setOperation = null;
  StatementList body;
}
{
  "for" "(" (init = VAR_INITIALIZATION())? ";" conditional = EXPRESSION() ";" (setOperation = SET_OPERATION())? ")" "{"
    body = STATEMENT_LIST()
  "}"
  
  { return new ForLoop(init, conditional, setOperation, body); }
}

// --------------------------------------------------------------------------------------------------------------------
// Expression - can be a function call or a binary expression
Expression EXPRESSION() :
{
  FunctionCall fc;
  Expression expr;
}
{
  LOOKAHEAD(3) fc = FUNCTION_CALL()
  {return new FunctionCall(fc.getName(), fc.getArgs());}
|
  LOOKAHEAD(3) expr = ADDITIVE_EXPRESSION()
  {return expr;}
}



// --------------------------------------------------------------------------------------------------------------------
// Addition and subtraction have precedence level 1 (lowest).
Expression ADDITIVE_EXPRESSION():
{
  Token op;
  Expression xa, xb;
}
{
  xa = MULTIPLICATIVE_EXPRESSION() (LOOKAHEAD(2) op = <("+" | "-")> xb = MULTIPLICATIVE_EXPRESSION() {xa = new BinaryExpression(xa, op.image, xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Multiplication and division have precedence level 2.
Expression MULTIPLICATIVE_EXPRESSION():
{
  Token op;
  Expression xa, xb;
}
{
  xa = SHIFT_EXPRESSION() (LOOKAHEAD(2) op = <("*" | "/" | "%")> xb = SHIFT_EXPRESSION() {xa = new BinaryExpression(xa, op.image, xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Shifts have precedence level 3.
Expression SHIFT_EXPRESSION():
{
  Token op;
  Expression xa, xb;
}
{
  xa = MAGNITUDE_EXPRESSION() (LOOKAHEAD(2) op = <("<<<" | ">>>" | "<<" | ">>")> xb = MAGNITUDE_EXPRESSION() {xa = new BinaryExpression(xa, op.image, xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Magnitude comparisons have precedence level 4.
Expression MAGNITUDE_EXPRESSION():
{
  Token op;
  Expression xa, xb;
}
{
  xa = EQUALITY_EXPRESSION() (LOOKAHEAD(2) op = <("<=" | ">=" | "<" | ">")> xb = EQUALITY_EXPRESSION() {xa = new BinaryExpression(xa, op.image, xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Equality comparisons have precedence level 5.
Expression EQUALITY_EXPRESSION():
{
  Token op;
  Expression xa, xb;
}
{
  xa = AND_EXPRESSION() (LOOKAHEAD(2) op = <("==" | "!=")> xb = AND_EXPRESSION() {xa = new BinaryExpression(xa, op.image, xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Binary AND has precedence level 6.
Expression AND_EXPRESSION():
{
  Expression xa, xb;
}
{
  xa = XOR_EXPRESSION() (LOOKAHEAD(2) "&" xb = XOR_EXPRESSION() {xa = new BinaryExpression(xa, "&", xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Binary XOR has precedence level 7.
Expression XOR_EXPRESSION():
{
  Expression xa, xb;
}
{
  xa = OR_EXPRESSION() (LOOKAHEAD(2) "^" xb = OR_EXPRESSION() {xa = new BinaryExpression(xa, "^", xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Binary OR has precedence level 8 (highest binary precedence).
Expression OR_EXPRESSION():
{
  Expression xa, xb;
}
{
  xa = UNARY_EXPRESSION() (LOOKAHEAD(2) "|" xb = UNARY_EXPRESSION() {xa = new BinaryExpression(xa, "|", xb);} )*
  {return xa;}
}

// --------------------------------------------------------------------------------------------------------------------
// Unary operations (? = bit count, !/- = bitwise negation/two's compliment negation) have precedence level 9 (highest)
Expression UNARY_EXPRESSION() :
{
  FunctionCall fc;
  Expression ex;
  Token op;
  VariableReference vr;
  ConfigReference cr;
  NumberVal nv;
}
{
  ("(" ex = EXPRESSION() ")")
  {return ex;}
|
  fc = FUNCTION_CALL()
  {return fc;}
|
  op = <("?" | "!" | "-")> ex = UNARY_EXPRESSION()
  {return new UnaryExpression(Operator.getOp(op.image), ex);}
|
  cr = CONFIG_REFERENCE()
  {return cr;}
|
  LOOKAHEAD(2) vr = VARIABLE_REFERENCE()
  {return vr;}
|
  nv = NUMBER()
  {return nv;}
  
}

// --------------------------------------------------------------------------------------------------------------------
// A reference to a variable in a config file
//   @ <FILE> : <VARIABLE>
ConfigReference CONFIG_REFERENCE() :
{
  Token cls;
  Token var;
}
{
  "@" cls = <IDENTIFIER> ":" var = <IDENTIFIER>
  {return new ConfigReference(cls.image, var.image);}
}

// --------------------------------------------------------------------------------------------------------------------
// A reference to a variable, can also include array index referencing
VariableReference VARIABLE_REFERENCE() :
{
  Token t;
  Expression ref;
  List<Expression> arrayRefs = new ArrayList<Expression>();
}
{
  t = <IDENTIFIER> ("[" ref = EXPRESSION() "]" {arrayRefs.add(ref);})*
  {return new VariableReference(t.image, arrayRefs);}
}

// --------------------------------------------------------------------------------------------------------------------
// A whole number value in either decimal or hexadecimal.
NumberVal NUMBER() :
{
  Token t;
}
{
  t = <LIT_NUMBER>
  {return new NumberVal(t.image);}
}

// --------------------------------------------------------------------------------------------------------------------
// A function call with its name and arguments
//   .NAME(EXPRESSION, EXPRESSION, ...)
FunctionCall FUNCTION_CALL() :
{
  Token name;
  ArgList argList = null;
}
{
  "." name = <IDENTIFIER> "(" ( argList = ARG_LIST() )? ")"
  { return new FunctionCall(name.image, ((argList == null) ? null : argList.getList())); }
}

// --------------------------------------------------------------------------------------------------------------------
// List of arguments, internal parser structure
ArgList ARG_LIST() :
{
  Expression expr;
  List<Expression> exprList = new ArrayList<Expression>();
}
{
  expr = EXPRESSION() {exprList.add(expr);} ("," expr = EXPRESSION() { exprList.add(expr); } )*
  { return new ArgList(exprList); }
}

